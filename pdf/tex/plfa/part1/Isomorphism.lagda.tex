\hypertarget{Isomorphism}{%
\chapter{Isomorphism: Isomorphism and Embedding}\label{Isomorphism}}

\begin{fence}
\begin{code}
module plfa.part1.Isomorphism where
\end{code}
\end{fence}

This section introduces isomorphism as a way of asserting that two types
are equal, and embedding as a way of asserting that one type is smaller
than another. We apply isomorphisms in the next chapter to demonstrate
that operations on types such as product and sum satisfy properties akin
to associativity, commutativity, and distributivity.

\hypertarget{imports}{%
\section{Imports}\label{imports}}

\begin{fence}
\begin{code}
import Relation.Binary.PropositionalEquality as Eq
open Eq using (_≡_; refl; cong; cong-app)
open Eq.≡-Reasoning
open import Data.Nat using (ℕ; zero; suc; _+_)
open import Data.Nat.Properties using (+-comm)
\end{code}
\end{fence}

\hypertarget{lambda-expressions}{%
\section{Lambda expressions}\label{lambda-expressions}}

The chapter begins with a few preliminaries that will be useful here and
elsewhere: lambda expressions, function composition, and extensionality.

\emph{Lambda expressions} provide a compact way to define functions
without naming them. A term of the form

\begin{myDisplay}
λ{ P₁ → N₁; ⋯ ; Pₙ → Nₙ }
\end{myDisplay}

is equivalent to a function \texttt{f} defined by the equations

\begin{myDisplay}
f P₁ = N₁
⋯
f Pₙ = Nₙ
\end{myDisplay}

where the \texttt{Pₙ} are patterns (left-hand sides of an equation) and
the \texttt{Nₙ} are expressions (right-hand side of an equation).

In the case that there is one equation and the pattern is a variable, we
may also use the syntax

\begin{myDisplay}
λ x → N
\end{myDisplay}

or

\begin{myDisplay}
λ (x : A) → N
\end{myDisplay}

both of which are equivalent to \texttt{λ\{x\ →\ N\}}. The latter allows
one to specify the domain of the function.

Often using an anonymous lambda expression is more convenient than using
a named function: it avoids a lengthy type declaration; and the
definition appears exactly where the function is used, so there is no
need for the writer to remember to declare it in advance, or for the
reader to search for the definition in the code.

\hypertarget{function-composition}{%
\section{Function composition}\label{function-composition}}

In what follows, we will make use of function composition:

\begin{fence}
\begin{code}
_∘_ : ∀ {A B C : Set} → (B → C) → (A → B) → (A → C)
(g ∘ f) x  = g (f x)
\end{code}
\end{fence}

Thus, \texttt{g\ ∘\ f} is the function that first applies \texttt{f} and
then applies \texttt{g}. An equivalent definition, exploiting lambda
expressions, is as follows:

\begin{fence}
\begin{code}
_∘′_ : ∀ {A B C : Set} → (B → C) → (A → B) → (A → C)
g ∘′ f  =  λ x → g (f x)
\end{code}
\end{fence}

\hypertarget{Isomorphism-extensionality}{%
\section{Extensionality}\label{Isomorphism-extensionality}}

Extensionality asserts that the only way to distinguish functions is by
applying them; if two functions applied to the same argument always
yield the same result, then they are the same function. It is the
converse of \texttt{cong-app}, as introduced
\protect\hyperlink{Equality-cong}{earlier}.

Agda does not presume extensionality, but we can postulate that it
holds:

\begin{fence}
\begin{code}
postulate
  extensionality : ∀ {A B : Set} {f g : A → B}
    → (∀ (x : A) → f x ≡ g x)
      -----------------------
    → f ≡ g
\end{code}
\end{fence}

Postulating extensionality does not lead to difficulties, as it is known
to be consistent with the theory that underlies Agda.

As an example, consider that we need results from two libraries, one
where addition is defined, as in Chapter
\protect\hyperlink{Naturals}{Naturals}, and one where it is defined the
other way around.

\begin{fence}
\begin{code}
_+′_ : ℕ → ℕ → ℕ
m +′ zero  = m
m +′ suc n = suc (m +′ n)
\end{code}
\end{fence}

Applying commutativity, it is easy to show that both operators always
return the same result given the same arguments:

\begin{fence}
\begin{code}
same-app : ∀ (m n : ℕ) → m +′ n ≡ m + n
same-app m n rewrite +-comm m n = helper m n
  where
  helper : ∀ (m n : ℕ) → m +′ n ≡ n + m
  helper m zero    = refl
  helper m (suc n) = cong suc (helper m n)
\end{code}
\end{fence}

However, it might be convenient to assert that the two operators are
actually indistinguishable. This we can do via two applications of
extensionality:

\begin{fence}
\begin{code}
same : _+′_ ≡ _+_
same = extensionality (λ m → extensionality (λ n → same-app m n))
\end{code}
\end{fence}

We occasionally need to postulate extensionality in what follows.

More generally, we may wish to postulate extensionality for dependent
functions.

\begin{fence}
\begin{code}
postulate
  ∀-extensionality : ∀ {A : Set} {B : A → Set} {f g : ∀(x : A) → B x}
    → (∀ (x : A) → f x ≡ g x)
      -----------------------
    → f ≡ g
\end{code}
\end{fence}

Here the type of \texttt{f} and \texttt{g} has changed from
\texttt{A\ →\ B} to \texttt{∀\ (x\ :\ A)\ →\ B\ x}, generalising
ordinary functions to dependent functions.

\hypertarget{isomorphism}{%
\section{Isomorphism}\label{isomorphism}}

Two sets are isomorphic if they are in one-to-one correspondence. Here
is a formal definition of isomorphism:

\begin{fence}
\begin{code}
infix 0 _≃_
record _≃_ (A B : Set) : Set where
  field
    to   : A → B
    from : B → A
    from∘to : ∀ (x : A) → from (to x) ≡ x
    to∘from : ∀ (y : B) → to (from y) ≡ y
open _≃_
\end{code}
\end{fence}

Let's unpack the definition. An isomorphism between sets \texttt{A} and
\texttt{B} consists of four things: + A function \texttt{to} from
\texttt{A} to \texttt{B}, + A function \texttt{from} from \texttt{B}
back to \texttt{A}, + Evidence \texttt{from∘to} asserting that
\texttt{from} is a \emph{left-inverse} for \texttt{to}, + Evidence
\texttt{to∘from} asserting that \texttt{from} is a \emph{right-inverse}
for \texttt{to}.

In particular, the third asserts that \texttt{from\ ∘\ to} is the
identity, and the fourth that \texttt{to\ ∘\ from} is the identity,
hence the names. The declaration \texttt{open\ \_≃\_} makes available
the names \texttt{to}, \texttt{from}, \texttt{from∘to}, and
\texttt{to∘from}, otherwise we would need to write \texttt{\_≃\_.to} and
so on.

The above is our first use of records. A record declaration behaves
similar to a single-constructor data declaration (there are minor
differences, which we discuss in
\protect\hyperlink{Connectives}{Connectives}):

\begin{fence}
\begin{code}
data _≃′_ (A B : Set): Set where
  mk-≃′ : ∀ (to : A → B) →
          ∀ (from : B → A) →
          ∀ (from∘to : (∀ (x : A) → from (to x) ≡ x)) →
          ∀ (to∘from : (∀ (y : B) → to (from y) ≡ y)) →
          A ≃′ B

to′ : ∀ {A B : Set} → (A ≃′ B) → (A → B)
to′ (mk-≃′ f g g∘f f∘g) = f

from′ : ∀ {A B : Set} → (A ≃′ B) → (B → A)
from′ (mk-≃′ f g g∘f f∘g) = g

from∘to′ : ∀ {A B : Set} → (A≃B : A ≃′ B) → (∀ (x : A) → from′ A≃B (to′ A≃B x) ≡ x)
from∘to′ (mk-≃′ f g g∘f f∘g) = g∘f

to∘from′ : ∀ {A B : Set} → (A≃B : A ≃′ B) → (∀ (y : B) → to′ A≃B (from′ A≃B y) ≡ y)
to∘from′ (mk-≃′ f g g∘f f∘g) = f∘g
\end{code}
\end{fence}

We construct values of the record type with the syntax

\begin{myDisplay}
record
  { to    = f
  ; from  = g
  ; from∘to = g∘f
  ; to∘from = f∘g
  }
\end{myDisplay}

which corresponds to using the constructor of the corresponding
inductive type

\begin{myDisplay}
mk-≃′ f g g∘f f∘g
\end{myDisplay}

where \texttt{f}, \texttt{g}, \texttt{g∘f}, and \texttt{f∘g} are values
of suitable types.

\hypertarget{isomorphism-is-an-equivalence}{%
\section{Isomorphism is an
equivalence}\label{isomorphism-is-an-equivalence}}

Isomorphism is an equivalence, meaning that it is reflexive, symmetric,
and transitive. To show isomorphism is reflexive, we take both
\texttt{to} and \texttt{from} to be the identity function:

\begin{fence}
\begin{code}
≃-refl : ∀ {A : Set}
    -----
  → A ≃ A
≃-refl =
  record
    { to      = λ{x → x}
    ; from    = λ{y → y}
    ; from∘to = λ{x → refl}
    ; to∘from = λ{y → refl}
    }
\end{code}
\end{fence}

In the above, \texttt{to} and \texttt{from} are both bound to identity
functions, and \texttt{from∘to} and \texttt{to∘from} are both bound to
functions that discard their argument and return \texttt{refl}. In this
case, \texttt{refl} alone is an adequate proof since for the left
inverse, \texttt{from\ (to\ x)} simplifies to \texttt{x}, and similarly
for the right inverse.

To show isomorphism is symmetric, we simply swap the roles of
\texttt{to} and \texttt{from}, and \texttt{from∘to} and
\texttt{to∘from}:

\begin{fence}
\begin{code}
≃-sym : ∀ {A B : Set}
  → A ≃ B
    -----
  → B ≃ A
≃-sym A≃B =
  record
    { to      = from A≃B
    ; from    = to   A≃B
    ; from∘to = to∘from A≃B
    ; to∘from = from∘to A≃B
    }
\end{code}
\end{fence}

To show isomorphism is transitive, we compose the \texttt{to} and
\texttt{from} functions, and use equational reasoning to combine the
inverses:

\begin{fence}
\begin{code}
≃-trans : ∀ {A B C : Set}
  → A ≃ B
  → B ≃ C
    -----
  → A ≃ C
≃-trans A≃B B≃C =
  record
    { to       = to   B≃C ∘ to   A≃B
    ; from     = from A≃B ∘ from B≃C
    ; from∘to  = λ{x →
        begin
          (from A≃B ∘ from B≃C) ((to B≃C ∘ to A≃B) x)
        ≡⟨⟩
          from A≃B (from B≃C (to B≃C (to A≃B x)))
        ≡⟨ cong (from A≃B) (from∘to B≃C (to A≃B x)) ⟩
          from A≃B (to A≃B x)
        ≡⟨ from∘to A≃B x ⟩
          x
        ∎}
    ; to∘from = λ{y →
        begin
          (to B≃C ∘ to A≃B) ((from A≃B ∘ from B≃C) y)
        ≡⟨⟩
          to B≃C (to A≃B (from A≃B (from B≃C y)))
        ≡⟨ cong (to B≃C) (to∘from A≃B (from B≃C y)) ⟩
          to B≃C (from B≃C y)
        ≡⟨ to∘from B≃C y ⟩
          y
        ∎}
     }
\end{code}
\end{fence}

\hypertarget{equational-reasoning-for-isomorphism}{%
\section{Equational reasoning for
isomorphism}\label{equational-reasoning-for-isomorphism}}

It is straightforward to support a variant of equational reasoning for
isomorphism. We essentially copy the previous definition of equality for
isomorphism. We omit the form that corresponds to \texttt{\_≡⟨⟩\_},
since trivial isomorphisms arise far less often than trivial equalities:

\begin{fence}
\begin{code}
module ≃-Reasoning where

  infix  1 ≃-begin_
  infixr 2 _≃⟨_⟩_
  infix  3 _≃-∎

  ≃-begin_ : ∀ {A B : Set}
    → A ≃ B
      -----
    → A ≃ B
  ≃-begin A≃B = A≃B

  _≃⟨_⟩_ : ∀ (A : Set) {B C : Set}
    → A ≃ B
    → B ≃ C
      -----
    → A ≃ C
  A ≃⟨ A≃B ⟩ B≃C = ≃-trans A≃B B≃C

  _≃-∎ : ∀ (A : Set)
      -----
    → A ≃ A
  A ≃-∎ = ≃-refl

open ≃-Reasoning
\end{code}
\end{fence}

\hypertarget{embedding}{%
\section{Embedding}\label{embedding}}

We also need the notion of \emph{embedding}, which is a weakening of
isomorphism. While an isomorphism shows that two types are in one-to-one
correspondence, an embedding shows that the first type is included in
the second; or, equivalently, that there is a many-to-one correspondence
between the second type and the first.

Here is the formal definition of embedding:

\begin{fence}
\begin{code}
infix 0 _≲_
record _≲_ (A B : Set) : Set where
  field
    to      : A → B
    from    : B → A
    from∘to : ∀ (x : A) → from (to x) ≡ x
open _≲_
\end{code}
\end{fence}

It is the same as an isomorphism, save that it lacks the
\texttt{to∘from} field. Hence, we know that \texttt{from} is
left-inverse to \texttt{to}, but not that \texttt{from} is right-inverse
to \texttt{to}.

Embedding is reflexive and transitive, but not symmetric. The proofs are
cut down versions of the similar proofs for isomorphism:

\begin{fence}
\begin{code}
≲-refl : ∀ {A : Set} → A ≲ A
≲-refl =
  record
    { to      = λ{x → x}
    ; from    = λ{y → y}
    ; from∘to = λ{x → refl}
    }

≲-trans : ∀ {A B C : Set} → A ≲ B → B ≲ C → A ≲ C
≲-trans A≲B B≲C =
  record
    { to      = λ{x → to   B≲C (to   A≲B x)}
    ; from    = λ{y → from A≲B (from B≲C y)}
    ; from∘to = λ{x →
        begin
          from A≲B (from B≲C (to B≲C (to A≲B x)))
        ≡⟨ cong (from A≲B) (from∘to B≲C (to A≲B x)) ⟩
          from A≲B (to A≲B x)
        ≡⟨ from∘to A≲B x ⟩
          x
        ∎}
     }
\end{code}
\end{fence}

It is also easy to see that if two types embed in each other, and the
embedding functions correspond, then they are isomorphic. This is a weak
form of anti-symmetry:

\begin{fence}
\begin{code}
≲-antisym : ∀ {A B : Set}
  → (A≲B : A ≲ B)
  → (B≲A : B ≲ A)
  → (to A≲B ≡ from B≲A)
  → (from A≲B ≡ to B≲A)
    -------------------
  → A ≃ B
≲-antisym A≲B B≲A to≡from from≡to =
  record
    { to      = to A≲B
    ; from    = from A≲B
    ; from∘to = from∘to A≲B
    ; to∘from = λ{y →
        begin
          to A≲B (from A≲B y)
        ≡⟨ cong (to A≲B) (cong-app from≡to y) ⟩
          to A≲B (to B≲A y)
        ≡⟨ cong-app to≡from (to B≲A y) ⟩
          from B≲A (to B≲A y)
        ≡⟨ from∘to B≲A y ⟩
          y
        ∎}
    }
\end{code}
\end{fence}

The first three components are copied from the embedding, while the last
combines the left inverse of \texttt{B\ ≲\ A} with the equivalences of
the \texttt{to} and \texttt{from} components from the two embeddings to
obtain the right inverse of the isomorphism.

\hypertarget{equational-reasoning-for-embedding}{%
\section{Equational reasoning for
embedding}\label{equational-reasoning-for-embedding}}

We can also support tabular reasoning for embedding, analogous to that
used for isomorphism:

\begin{fence}
\begin{code}
module ≲-Reasoning where

  infix  1 ≲-begin_
  infixr 2 _≲⟨_⟩_
  infix  3 _≲-∎

  ≲-begin_ : ∀ {A B : Set}
    → A ≲ B
      -----
    → A ≲ B
  ≲-begin A≲B = A≲B

  _≲⟨_⟩_ : ∀ (A : Set) {B C : Set}
    → A ≲ B
    → B ≲ C
      -----
    → A ≲ C
  A ≲⟨ A≲B ⟩ B≲C = ≲-trans A≲B B≲C

  _≲-∎ : ∀ (A : Set)
      -----
    → A ≲ A
  A ≲-∎ = ≲-refl

open ≲-Reasoning
\end{code}
\end{fence}

\hypertarget{exercise--implies--practice}{%
\subsubsection{\texorpdfstring{Exercise \texttt{≃-implies-≲}
(practice)}{Exercise ≃-implies-≲ (practice)}}\label{exercise--implies--practice}}

Show that every isomorphism implies an embedding.

\begin{fence}
\begin{code}
postulate
  ≃-implies-≲ : ∀ {A B : Set}
    → A ≃ B
      -----
    → A ≲ B
\end{code}
\end{fence}

\begin{fence}
\begin{code}
-- Your code goes here
\end{code}
\end{fence}

\hypertarget{Isomorphism-iff}{%
\subsubsection{\texorpdfstring{Exercise \texttt{\_⇔\_}
(practice)}{Exercise \_⇔\_ (practice)}}\label{Isomorphism-iff}}

Define equivalence of propositions (also known as ``if and only if'') as
follows:

\begin{fence}
\begin{code}
record _⇔_ (A B : Set) : Set where
  field
    to   : A → B
    from : B → A
\end{code}
\end{fence}

Show that equivalence is reflexive, symmetric, and transitive.

\begin{fence}
\begin{code}
-- Your code goes here
\end{code}
\end{fence}

\hypertarget{Isomorphism-Bin-embedding}{%
\subsubsection{\texorpdfstring{Exercise \texttt{Bin-embedding}
(stretch)}{Exercise Bin-embedding (stretch)}}\label{Isomorphism-Bin-embedding}}

Recall that Exercises \protect\hyperlink{Naturals-Bin}{Bin} and
\protect\hyperlink{Induction-Bin-laws}{Bin-laws} define a datatype
\texttt{Bin} of bitstrings representing natural numbers, and asks you to
define the following functions and predicates:

\begin{myDisplay}
to : ℕ → Bin
from : Bin → ℕ
\end{myDisplay}

which satisfy the following property:

\begin{myDisplay}
from (to n) ≡ n
\end{myDisplay}

Using the above, establish that there is an embedding of \texttt{ℕ} into
\texttt{Bin}.

\begin{fence}
\begin{code}
-- Your code goes here
\end{code}
\end{fence}

Why do \texttt{to} and \texttt{from} not form an isomorphism?

\hypertarget{standard-library}{%
\section{Standard library}\label{standard-library}}

Definitions similar to those in this chapter can be found in the
standard library:

\begin{fence}
\begin{code}
import Function using (_∘_)
import Function.Inverse using (_↔_)
import Function.LeftInverse using (_↞_)
\end{code}
\end{fence}

The standard library \texttt{\_↔\_} and \texttt{\_↞\_} correspond to our
\texttt{\_≃\_} and \texttt{\_≲\_}, respectively, but those in the
standard library are less convenient, since they depend on a nested
record structure and are parameterised with regard to an arbitrary
notion of equivalence.

\hypertarget{unicode}{%
\section{Unicode}\label{unicode}}

This chapter uses the following unicode:

\begin{myDisplay}
∘  U+2218  RING OPERATOR (\o, \circ, \comp)
λ  U+03BB  GREEK SMALL LETTER LAMBDA (\lambda, \Gl)
≃  U+2243  ASYMPTOTICALLY EQUAL TO (\~-)
≲  U+2272  LESS-THAN OR EQUIVALENT TO (\<~)
⇔  U+21D4  LEFT RIGHT DOUBLE ARROW (\<=>)
\end{myDisplay}

