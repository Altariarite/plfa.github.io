\hypertarget{Decidable}{%
\chapter{Decidable: Booleans and decision procedures}\label{Decidable}}

\begin{fence}
\begin{code}
module plfa.part1.Decidable where
\end{code}
\end{fence}

We have a choice as to how to represent relations: as an inductive data
type of \emph{evidence} that the relation holds, or as a function that
\emph{computes} whether the relation holds. Here we explore the relation
between these choices. We first explore the familiar notion of
\emph{booleans}, but later discover that these are best avoided in
favour of a new notion of \emph{decidable}.

\hypertarget{imports}{%
\section{Imports}\label{imports}}

\begin{fence}
\begin{code}
import Relation.Binary.PropositionalEquality as Eq
open Eq using (_≡_; refl)
open Eq.≡-Reasoning
open import Data.Nat using (ℕ; zero; suc)
open import Data.Product using (_×_) renaming (_,_ to ⟨_,_⟩)
open import Data.Sum using (_⊎_; inj₁; inj₂)
open import Relation.Nullary using (¬_)
open import Relation.Nullary.Negation using ()
  renaming (contradiction to ¬¬-intro)
open import Data.Unit using (⊤; tt)
open import Data.Empty using (⊥; ⊥-elim)
open import plfa.part1.Relations using (_<_; z<s; s<s)
open import plfa.part1.Isomorphism using (_⇔_)
\end{code}
\end{fence}

\hypertarget{evidence-vs-computation}{%
\section{Evidence vs Computation}\label{evidence-vs-computation}}

Recall that Chapter \protect\hyperlink{Relations}{Relations} defined
comparison as an inductive datatype, which provides \emph{evidence} that
one number is less than or equal to another:

\begin{fence}
\begin{code}
infix 4 _≤_

data _≤_ : ℕ → ℕ → Set where

  z≤n : ∀ {n : ℕ}
      --------
    → zero ≤ n

  s≤s : ∀ {m n : ℕ}
    → m ≤ n
      -------------
    → suc m ≤ suc n
\end{code}
\end{fence}

For example, we can provide evidence that \texttt{2\ ≤\ 4}, and show
there is no possible evidence that \texttt{4\ ≤\ 2}:

\begin{fence}
\begin{code}
2≤4 : 2 ≤ 4
2≤4 = s≤s (s≤s z≤n)

¬4≤2 : ¬ (4 ≤ 2)
¬4≤2 (s≤s (s≤s ()))
\end{code}
\end{fence}

The occurrence of \texttt{()} attests to the fact that there is no
possible evidence for \texttt{2\ ≤\ 0}, which \texttt{z≤n} cannot match
(because \texttt{2} is not \texttt{zero}) and \texttt{s≤s} cannot match
(because \texttt{0} cannot match \texttt{suc\ n}).

An alternative, which may seem more familiar, is to define a type of
booleans:

\begin{fence}
\begin{code}
data Bool : Set where
  true  : Bool
  false : Bool
\end{code}
\end{fence}

Given booleans, we can define a function of two numbers that
\emph{computes} to \texttt{true} if the comparison holds and to
\texttt{false} otherwise:

\begin{fence}
\begin{code}
infix 4 _≤ᵇ_

_≤ᵇ_ : ℕ → ℕ → Bool
zero ≤ᵇ n       =  true
suc m ≤ᵇ zero   =  false
suc m ≤ᵇ suc n  =  m ≤ᵇ n
\end{code}
\end{fence}

The first and last clauses of this definition resemble the two
constructors of the corresponding inductive datatype, while the middle
clause arises because there is no possible evidence that
\texttt{suc\ m\ ≤\ zero} for any \texttt{m}. For example, we can compute
that \texttt{2\ ≤ᵇ\ 4} holds, and we can compute that \texttt{4\ ≤ᵇ\ 2}
does not hold:

\begin{fence}
\begin{code}
_ : (2 ≤ᵇ 4) ≡ true
_ =
  begin
    2 ≤ᵇ 4
  ≡⟨⟩
    1 ≤ᵇ 3
  ≡⟨⟩
    0 ≤ᵇ 2
  ≡⟨⟩
    true
  ∎

_ : (4 ≤ᵇ 2) ≡ false
_ =
  begin
    4 ≤ᵇ 2
  ≡⟨⟩
    3 ≤ᵇ 1
  ≡⟨⟩
    2 ≤ᵇ 0
  ≡⟨⟩
    false
  ∎
\end{code}
\end{fence}

In the first case, it takes two steps to reduce the first argument to
zero, and one more step to compute true, corresponding to the two uses
of \texttt{s≤s} and the one use of \texttt{z≤n} when providing evidence
that \texttt{2\ ≤\ 4}. In the second case, it takes two steps to reduce
the second argument to zero, and one more step to compute false,
corresponding to the two uses of \texttt{s≤s} and the one use of
\texttt{()} when showing there can be no evidence that \texttt{4\ ≤\ 2}.

\hypertarget{relating-evidence-and-computation}{%
\section{Relating evidence and
computation}\label{relating-evidence-and-computation}}

We would hope to be able to show these two approaches are related, and
indeed we can. First, we define a function that lets us map from the
computation world to the evidence world:

\begin{fence}
\begin{code}
T : Bool → Set
T true   =  ⊤
T false  =  ⊥
\end{code}
\end{fence}

Recall that \texttt{⊤} is the unit type which contains the single
element \texttt{tt}, and the \texttt{⊥} is the empty type which contains
no values. (Also note that \texttt{T} is a capital letter t, and
distinct from \texttt{⊤}.) If \texttt{b} is of type \texttt{Bool}, then
\texttt{tt} provides evidence that \texttt{T\ b} holds if \texttt{b} is
true, while there is no possible evidence that \texttt{T\ b} holds if
\texttt{b} is false.

Another way to put this is that \texttt{T\ b} is inhabited exactly when
\texttt{b\ ≡\ true} is inhabited. In the forward direction, we need to
do a case analysis on the boolean \texttt{b}:

\begin{fence}
\begin{code}
T→≡ : ∀ (b : Bool) → T b → b ≡ true
T→≡ true tt   =  refl
T→≡ false ()
\end{code}
\end{fence}

If \texttt{b} is true then \texttt{T\ b} is inhabited by \texttt{tt} and
\texttt{b\ ≡\ true} is inhabited by \texttt{refl}, while if \texttt{b}
is false then \texttt{T\ b} in uninhabited.

In the reverse direction, there is no need for a case analysis on the
boolean \texttt{b}:

\begin{fence}
\begin{code}
≡→T : ∀ {b : Bool} → b ≡ true → T b
≡→T refl  =  tt
\end{code}
\end{fence}

If \texttt{b\ ≡\ true} is inhabited by \texttt{refl} we know that
\texttt{b} is \texttt{true} and hence \texttt{T\ b} is inhabited by
\texttt{tt}.

Now we can show that \texttt{T\ (m\ ≤ᵇ\ n)} is inhabited exactly when
\texttt{m\ ≤\ n} is inhabited.

In the forward direction, we consider the three clauses in the
definition of \texttt{\_≤ᵇ\_}:

\begin{fence}
\begin{code}
≤ᵇ→≤ : ∀ (m n : ℕ) → T (m ≤ᵇ n) → m ≤ n
≤ᵇ→≤ zero    n       tt  =  z≤n
≤ᵇ→≤ (suc m) zero    ()
≤ᵇ→≤ (suc m) (suc n) t   =  s≤s (≤ᵇ→≤ m n t)
\end{code}
\end{fence}

In the first clause, we immediately have that \texttt{zero\ ≤ᵇ\ n} is
true, so \texttt{T\ (m\ ≤ᵇ\ n)} is evidenced by \texttt{tt}, and
correspondingly \texttt{m\ ≤\ n} is evidenced by \texttt{z≤n}. In the
middle clause, we immediately have that \texttt{suc\ m\ ≤ᵇ\ zero} is
false, and hence \texttt{T\ (m\ ≤ᵇ\ n)} is empty, so we need not provide
evidence that \texttt{m\ ≤\ n}, which is just as well since there is no
such evidence. In the last clause, we have that
\texttt{suc\ m\ ≤ᵇ\ suc\ n} recurses to \texttt{m\ ≤ᵇ\ n}. We let
\texttt{t} be the evidence of \texttt{T\ (suc\ m\ ≤ᵇ\ suc\ n)} if it
exists, which, by definition of \texttt{\_≤ᵇ\_}, will also be evidence
of \texttt{T\ (m\ ≤ᵇ\ n)}. We recursively invoke the function to get
evidence that \texttt{m\ ≤\ n}, which \texttt{s≤s} converts to evidence
that \texttt{suc\ m\ ≤\ suc\ n}.

In the reverse direction, we consider the possible forms of evidence
that \texttt{m\ ≤\ n}:

\begin{fence}
\begin{code}
≤→≤ᵇ : ∀ {m n : ℕ} → m ≤ n → T (m ≤ᵇ n)
≤→≤ᵇ z≤n        =  tt
≤→≤ᵇ (s≤s m≤n)  =  ≤→≤ᵇ m≤n
\end{code}
\end{fence}

If the evidence is \texttt{z≤n} then we immediately have that
\texttt{zero\ ≤ᵇ\ n} is true, so \texttt{T\ (m\ ≤ᵇ\ n)} is evidenced by
\texttt{tt}. If the evidence is \texttt{s≤s} applied to \texttt{m≤n},
then \texttt{suc\ m\ ≤ᵇ\ suc\ n} reduces to \texttt{m\ ≤ᵇ\ n}, and we
may recursively invoke the function to produce evidence that
\texttt{T\ (m\ ≤ᵇ\ n)}.

The forward proof has one more clause than the reverse proof, precisely
because in the forward proof we need clauses corresponding to the
comparison yielding both true and false, while in the reverse proof we
only need clauses corresponding to the case where there is evidence that
the comparison holds. This is exactly why we tend to prefer the evidence
formulation to the computation formulation, because it allows us to do
less work: we consider only cases where the relation holds, and can
ignore those where it does not.

On the other hand, sometimes the computation formulation may be just
what we want. Given a non-obvious relation over large values, it might
be handy to have the computer work out the answer for us. Fortunately,
rather than choosing between \emph{evidence} and \emph{computation},
there is a way to get the benefits of both.

\hypertarget{the-best-of-both-worlds}{%
\section{The best of both worlds}\label{the-best-of-both-worlds}}

A function that returns a boolean returns exactly a single bit of
information: does the relation hold or does it not? Conversely, the
evidence approach tells us exactly why the relation holds, but we are
responsible for generating the evidence. But it is easy to define a type
that combines the benefits of both approaches. It is called
\texttt{Dec\ A}, where \texttt{Dec} is short for \emph{decidable}:

\begin{fence}
\begin{code}
data Dec (A : Set) : Set where
  yes :   A → Dec A
  no  : ¬ A → Dec A
\end{code}
\end{fence}

Like booleans, the type has two constructors. A value of type
\texttt{Dec\ A} is either of the form \texttt{yes\ x}, where \texttt{x}
provides evidence that \texttt{A} holds, or of the form \texttt{no\ ¬x},
where \texttt{¬x} provides evidence that \texttt{A} cannot hold (that
is, \texttt{¬x} is a function which given evidence of \texttt{A} yields
a contradiction).

For example, we define a function \texttt{\_≤?\_} which given two
numbers decides whether one is less than or equal to the other, and
provides evidence to justify its conclusion.

First, we introduce two functions useful for constructing evidence that
an inequality does not hold:

\begin{fence}
\begin{code}
¬s≤z : ∀ {m : ℕ} → ¬ (suc m ≤ zero)
¬s≤z ()

¬s≤s : ∀ {m n : ℕ} → ¬ (m ≤ n) → ¬ (suc m ≤ suc n)
¬s≤s ¬m≤n (s≤s m≤n) = ¬m≤n m≤n
\end{code}
\end{fence}

The first of these asserts that \texttt{¬\ (suc\ m\ ≤\ zero)}, and
follows by absurdity, since any evidence of inequality has the form
\texttt{zero\ ≤\ n} or \texttt{suc\ m\ ≤\ suc\ n}, neither of which
match \texttt{suc\ m\ ≤\ zero}. The second of these takes evidence
\texttt{¬m≤n} of \texttt{¬\ (m\ ≤\ n)} and returns a proof of
\texttt{¬\ (suc\ m\ ≤\ suc\ n)}. Any evidence of
\texttt{suc\ m\ ≤\ suc\ n} must have the form \texttt{s≤s\ m≤n} where
\texttt{m≤n} is evidence that \texttt{m\ ≤\ n}. Hence, we have a
contradiction, evidenced by \texttt{¬m≤n\ m≤n}.

Using these, it is straightforward to decide an inequality:

\begin{fence}
\begin{code}
_≤?_ : ∀ (m n : ℕ) → Dec (m ≤ n)
zero  ≤? n                   =  yes z≤n
suc m ≤? zero                =  no ¬s≤z
suc m ≤? suc n with m ≤? n
...               | yes m≤n  =  yes (s≤s m≤n)
...               | no ¬m≤n  =  no (¬s≤s ¬m≤n)
\end{code}
\end{fence}

As with \texttt{\_≤ᵇ\_}, the definition has three clauses. In the first
clause, it is immediate that \texttt{zero\ ≤\ n} holds, and it is
evidenced by \texttt{z≤n}. In the second clause, it is immediate that
\texttt{suc\ m\ ≤\ zero} does not hold, and it is evidenced by
\texttt{¬s≤z}. In the third clause, to decide whether
\texttt{suc\ m\ ≤\ suc\ n} holds we recursively invoke
\texttt{m\ ≤?\ n}. There are two possibilities. In the \texttt{yes} case
it returns evidence \texttt{m≤n} that \texttt{m\ ≤\ n}, and
\texttt{s≤s\ m≤n} provides evidence that \texttt{suc\ m\ ≤\ suc\ n}. In
the \texttt{no} case it returns evidence \texttt{¬m≤n} that
\texttt{¬\ (m\ ≤\ n)}, and \texttt{¬s≤s\ ¬m≤n} provides evidence that
\texttt{¬\ (suc\ m\ ≤\ suc\ n)}.

When we wrote \texttt{\_≤ᵇ\_}, we had to write two other functions,
\texttt{≤ᵇ→≤} and \texttt{≤→≤ᵇ}, in order to show that it was correct.
In contrast, the definition of \texttt{\_≤?\_} proves itself correct, as
attested to by its type. The code of \texttt{\_≤?\_} is far more compact
than the combined code of \texttt{\_≤ᵇ\_}, \texttt{≤ᵇ→≤}, and
\texttt{≤→≤ᵇ}. As we will later show, if you really want the latter
three, it is easy to derive them from \texttt{\_≤?\_}.

We can use our new function to \emph{compute} the \emph{evidence} that
earlier we had to think up on our own:

\begin{fence}
\begin{code}
_ : 2 ≤? 4 ≡ yes (s≤s (s≤s z≤n))
_ = refl

_ : 4 ≤? 2 ≡ no (¬s≤s (¬s≤s ¬s≤z))
_ = refl
\end{code}
\end{fence}

You can check that Agda will indeed compute these values. Typing
\texttt{C-c\ C-n} and providing \texttt{2\ ≤?\ 4} or \texttt{4\ ≤?\ 2}
as the requested expression causes Agda to print the values given above.

(A subtlety: if we do not define \texttt{¬s≤z} and \texttt{¬s≤s} as
top-level functions, but instead use inline anonymous functions then
Agda may have trouble normalising evidence of negation.)

\hypertarget{exercise-__-recommended}{%
\subsubsection{\texorpdfstring{Exercise \texttt{\_\textless{}?\_}
(recommended)}{Exercise \_\textless?\_ (recommended)}}\label{exercise-__-recommended}}

Analogous to the function above, define a function to decide strict
inequality:

\begin{fence}
\begin{code}
postulate
  _<?_ : ∀ (m n : ℕ) → Dec (m < n)
\end{code}
\end{fence}

\begin{fence}
\begin{code}
-- Your code goes here
\end{code}
\end{fence}

\hypertarget{exercise-_ux2115_-practice}{%
\subsubsection{\texorpdfstring{Exercise \texttt{\_≡ℕ?\_}
(practice)}{Exercise \_≡ℕ?\_ (practice)}}\label{exercise-_ux2115_-practice}}

Define a function to decide whether two naturals are equal:

\begin{fence}
\begin{code}
postulate
  _≡ℕ?_ : ∀ (m n : ℕ) → Dec (m ≡ n)
\end{code}
\end{fence}

\begin{fence}
\begin{code}
-- Your code goes here
\end{code}
\end{fence}

\hypertarget{decidables-from-booleans-and-booleans-from-decidables}{%
\section{Decidables from booleans, and booleans from
decidables}\label{decidables-from-booleans-and-booleans-from-decidables}}

Curious readers might wonder if we could reuse the definition of
\texttt{m\ ≤ᵇ\ n}, together with the proofs that it is equivalent to
\texttt{m\ ≤\ n}, to show decidability. Indeed, we can do so as follows:

\begin{fence}
\begin{code}
_≤?′_ : ∀ (m n : ℕ) → Dec (m ≤ n)
m ≤?′ n with m ≤ᵇ n | ≤ᵇ→≤ m n | ≤→≤ᵇ {m} {n}
...        | true   | p        | _            = yes (p tt)
...        | false  | _        | ¬p           = no ¬p
\end{code}
\end{fence}

If \texttt{m\ ≤ᵇ\ n} is true then \texttt{≤ᵇ→≤} yields a proof that
\texttt{m\ ≤\ n} holds, while if it is false then \texttt{≤→≤ᵇ} takes a
proof that \texttt{m\ ≤\ n} holds into a contradiction.

The triple binding of the \texttt{with} clause in this proof is
essential. If instead we wrote:

\begin{myDisplay}
_≤?″_ : ∀ (m n : ℕ) → Dec (m ≤ n)
m ≤?″ n with m ≤ᵇ n
... | true   =  yes (≤ᵇ→≤ m n tt)
... | false  =  no (≤→≤ᵇ {m} {n})
\end{myDisplay}

then Agda would make two complaints, one for each clause:

\begin{myDisplay}
⊤ !=< (T (m ≤ᵇ n)) of type Set
when checking that the expression tt has type T (m ≤ᵇ n)

T (m ≤ᵇ n) !=< ⊥ of type Set
when checking that the expression ≤→≤ᵇ {m} {n} has type ¬ m ≤ n
\end{myDisplay}

Putting the expressions into the \texttt{with} clause permits Agda to
exploit the fact that \texttt{T\ (m\ ≤ᵇ\ n)} is \texttt{⊤} when
\texttt{m\ ≤ᵇ\ n} is true, and that \texttt{T\ (m\ ≤ᵇ\ n)} is \texttt{⊥}
when \texttt{m\ ≤ᵇ\ n} is false.

However, overall it is simpler to just define \texttt{\_≤?\_} directly,
as in the previous section. If one really wants \texttt{\_≤ᵇ\_}, then it
and its properties are easily derived from \texttt{\_≤?\_}, as we will
now show.

Erasure takes a decidable value to a boolean:

\begin{fence}
\begin{code}
⌊_⌋ : ∀ {A : Set} → Dec A → Bool
⌊ yes x ⌋  =  true
⌊ no ¬x ⌋  =  false
\end{code}
\end{fence}

Using erasure, we can easily derive \texttt{\_≤ᵇ\_} from
\texttt{\_≤?\_}:

\begin{fence}
\begin{code}
_≤ᵇ′_ : ℕ → ℕ → Bool
m ≤ᵇ′ n  =  ⌊ m ≤? n ⌋
\end{code}
\end{fence}

Further, if \texttt{D} is a value of type \texttt{Dec\ A}, then
\texttt{T\ ⌊\ D\ ⌋} is inhabited exactly when \texttt{A} is inhabited:

\begin{fence}
\begin{code}
toWitness : ∀ {A : Set} {D : Dec A} → T ⌊ D ⌋ → A
toWitness {A} {yes x} tt  =  x
toWitness {A} {no ¬x} ()

fromWitness : ∀ {A : Set} {D : Dec A} → A → T ⌊ D ⌋
fromWitness {A} {yes x} _  =  tt
fromWitness {A} {no ¬x} x  =  ¬x x
\end{code}
\end{fence}

Using these, we can easily derive that \texttt{T\ (m\ ≤ᵇ′\ n)} is
inhabited exactly when \texttt{m\ ≤\ n} is inhabited:

\begin{fence}
\begin{code}
≤ᵇ′→≤ : ∀ {m n : ℕ} → T (m ≤ᵇ′ n) → m ≤ n
≤ᵇ′→≤  =  toWitness

≤→≤ᵇ′ : ∀ {m n : ℕ} → m ≤ n → T (m ≤ᵇ′ n)
≤→≤ᵇ′  =  fromWitness
\end{code}
\end{fence}

In summary, it is usually best to eschew booleans and rely on
decidables. If you need booleans, they and their properties are easily
derived from the corresponding decidables.

\hypertarget{logical-connectives}{%
\section{Logical connectives}\label{logical-connectives}}

Most readers will be familiar with the logical connectives for booleans.
Each of these extends to decidables.

The conjunction of two booleans is true if both are true, and false if
either is false:

\begin{fence}
\begin{code}
infixr 6 _∧_

_∧_ : Bool → Bool → Bool
true  ∧ true  = true
false ∧ _     = false
_     ∧ false = false
\end{code}
\end{fence}

In Emacs, the left-hand side of the third equation displays in grey,
indicating that the order of the equations determines which of the
second or the third can match. However, regardless of which matches the
answer is the same.

Correspondingly, given two decidable propositions, we can decide their
conjunction:

\begin{fence}
\begin{code}
infixr 6 _×-dec_

_×-dec_ : ∀ {A B : Set} → Dec A → Dec B → Dec (A × B)
yes x ×-dec yes y = yes ⟨ x , y ⟩
no ¬x ×-dec _     = no λ{ ⟨ x , y ⟩ → ¬x x }
_     ×-dec no ¬y = no λ{ ⟨ x , y ⟩ → ¬y y }
\end{code}
\end{fence}

The conjunction of two propositions holds if they both hold, and its
negation holds if the negation of either holds. If both hold, then we
pair the evidence for each to yield evidence of the conjunct. If the
negation of either holds, assuming the conjunct will lead to a
contradiction.

Again in Emacs, the left-hand side of the third equation displays in
grey, indicating that the order of the equations determines which of the
second or the third can match. This time the answer is different
depending on which matches; if both conjuncts fail to hold we pick the
first to yield the contradiction, but it would be equally valid to pick
the second.

The disjunction of two booleans is true if either is true, and false if
both are false:

\begin{fence}
\begin{code}
infixr 5 _∨_

_∨_ : Bool → Bool → Bool
true  ∨ _      = true
_     ∨ true   = true
false ∨ false  = false
\end{code}
\end{fence}

In Emacs, the left-hand side of the second equation displays in grey,
indicating that the order of the equations determines which of the first
or the second can match. However, regardless of which matches the answer
is the same.

Correspondingly, given two decidable propositions, we can decide their
disjunction:

\begin{fence}
\begin{code}
infixr 5 _⊎-dec_

_⊎-dec_ : ∀ {A B : Set} → Dec A → Dec B → Dec (A ⊎ B)
yes x ⊎-dec _     = yes (inj₁ x)
_     ⊎-dec yes y = yes (inj₂ y)
no ¬x ⊎-dec no ¬y = no λ{ (inj₁ x) → ¬x x ; (inj₂ y) → ¬y y }
\end{code}
\end{fence}

The disjunction of two propositions holds if either holds, and its
negation holds if the negation of both hold. If either holds, we inject
the evidence to yield evidence of the disjunct. If the negation of both
hold, assuming either disjunct will lead to a contradiction.

Again in Emacs, the left-hand side of the second equation displays in
grey, indicating that the order of the equations determines which of the
first or the second can match. This time the answer is different
depending on which matches; if both disjuncts hold we pick the first,
but it would be equally valid to pick the second.

The negation of a boolean is false if its argument is true, and vice
versa:

\begin{fence}
\begin{code}
not : Bool → Bool
not true  = false
not false = true
\end{code}
\end{fence}

Correspondingly, given a decidable proposition, we can decide its
negation:

\begin{fence}
\begin{code}
¬? : ∀ {A : Set} → Dec A → Dec (¬ A)
¬? (yes x)  =  no (¬¬-intro x)
¬? (no ¬x)  =  yes ¬x
\end{code}
\end{fence}

We simply swap yes and no. In the first equation, the right-hand side
asserts that the negation of \texttt{¬\ A} holds, in other words, that
\texttt{¬\ ¬\ A} holds, which is an easy consequence of the fact that
\texttt{A} holds.

There is also a slightly less familiar connective, corresponding to
implication:

\begin{fence}
\begin{code}
_⊃_ : Bool → Bool → Bool
_     ⊃ true   =  true
false ⊃ _      =  true
true  ⊃ false  =  false
\end{code}
\end{fence}

One boolean implies another if whenever the first is true then the
second is true. Hence, the implication of two booleans is true if the
second is true or the first is false, and false if the first is true and
the second is false. In Emacs, the left-hand side of the second equation
displays in grey, indicating that the order of the equations determines
which of the first or the second can match. However, regardless of which
matches the answer is the same.

Correspondingly, given two decidable propositions, we can decide if the
first implies the second:

\begin{fence}
\begin{code}
_→-dec_ : ∀ {A B : Set} → Dec A → Dec B → Dec (A → B)
_     →-dec yes y  =  yes (λ _ → y)
no ¬x →-dec _      =  yes (λ x → ⊥-elim (¬x x))
yes x →-dec no ¬y  =  no (λ f → ¬y (f x))
\end{code}
\end{fence}

The implication holds if either the second holds or the negation of the
first holds, and its negation holds if the first holds and the negation
of the second holds. Evidence for the implication is a function from
evidence of the first to evidence of the second. If the second holds,
the function returns the evidence for it. If the negation of the first
holds, the function takes the evidence of the first and derives a
contradiction. If the first holds and the negation of the second holds,
given evidence of the implication we must derive a contradiction; we
apply the evidence of the implication \texttt{f} to the evidence of the
first \texttt{x}, yielding a contradiction with the evidence \texttt{¬y}
of the negation of the second.

Again in Emacs, the left-hand side of the second equation displays in
grey, indicating that the order of the equations determines which of the
first or the second can match. This time the answer is different
depending on which matches; but either is equally valid.

\hypertarget{exercise-erasure-practice}{%
\subsubsection{\texorpdfstring{Exercise \texttt{erasure}
(practice)}{Exercise erasure (practice)}}\label{exercise-erasure-practice}}

Show that erasure relates corresponding boolean and decidable
operations:

\begin{fence}
\begin{code}
postulate
  ∧-× : ∀ {A B : Set} (x : Dec A) (y : Dec B) → ⌊ x ⌋ ∧ ⌊ y ⌋ ≡ ⌊ x ×-dec y ⌋
  ∨-⊎ : ∀ {A B : Set} (x : Dec A) (y : Dec B) → ⌊ x ⌋ ∨ ⌊ y ⌋ ≡ ⌊ x ⊎-dec y ⌋
  not-¬ : ∀ {A : Set} (x : Dec A) → not ⌊ x ⌋ ≡ ⌊ ¬? x ⌋
\end{code}
\end{fence}

\hypertarget{exercise-iff-erasure-recommended}{%
\subsubsection{\texorpdfstring{Exercise \texttt{iff-erasure}
(recommended)}{Exercise iff-erasure (recommended)}}\label{exercise-iff-erasure-recommended}}

Give analogues of the \texttt{\_⇔\_} operation from Chapter
\protect\hyperlink{Isomorphism-iff}{Isomorphism}, operation on booleans
and decidables, and also show the corresponding erasure:

\begin{fence}
\begin{code}
postulate
  _iff_ : Bool → Bool → Bool
  _⇔-dec_ : ∀ {A B : Set} → Dec A → Dec B → Dec (A ⇔ B)
  iff-⇔ : ∀ {A B : Set} (x : Dec A) (y : Dec B) → ⌊ x ⌋ iff ⌊ y ⌋ ≡ ⌊ x ⇔-dec y ⌋
\end{code}
\end{fence}

\begin{fence}
\begin{code}
-- Your code goes here
\end{code}
\end{fence}

\hypertarget{Decidable-proof-by-reflection}{%
\section{Proof by reflection}\label{Decidable-proof-by-reflection}}

Let's revisit our definition of monus from Chapter
\protect\hyperlink{Naturals}{Naturals}. If we subtract a larger number
from a smaller number, we take the result to be zero. We had to do
something, after all. What could we have done differently? We could have
defined a \emph{guarded} version of minus, a function which subtracts
\texttt{n} from \texttt{m} only if \texttt{n\ ≤\ m}:

\begin{fence}
\begin{code}
minus : (m n : ℕ) (n≤m : n ≤ m) → ℕ
minus m       zero    _         = m
minus (suc m) (suc n) (s≤s n≤m) = minus m n n≤m
\end{code}
\end{fence}

Unfortunately, it is painful to use, since we have to explicitly provide
the proof that \texttt{n\ ≤\ m}:

\begin{fence}
\begin{code}
_ : minus 5 3 (s≤s (s≤s (s≤s z≤n))) ≡ 2
_ = refl
\end{code}
\end{fence}

We cannot solve this problem in general, but in the scenario above, we
happen to know the two numbers \emph{statically}. In that case, we can
use a technique called \emph{proof by reflection}. Essentially, we can
ask Agda to run the decidable equality \texttt{n\ ≤?\ m} while type
checking, and make sure that \texttt{n\ ≤\ m}!

We do this by using a feature of implicits. Agda will fill in an
implicit of a record type if it can fill in all its fields. So Agda will
\emph{always} manage to fill in an implicit of an \emph{empty} record
type, since there aren't any fields after all. This is why \texttt{⊤} is
defined as an empty record.

The trick is to have an implicit argument of the type
\texttt{T\ ⌊\ n\ ≤?\ m\ ⌋}. Let's go through what this means
step-by-step. First, we run the decision procedure, \texttt{n\ ≤?\ m}.
This provides us with evidence whether \texttt{n\ ≤\ m} holds or not. We
erase the evidence to a boolean. Finally, we apply \texttt{T}. Recall
that \texttt{T} maps booleans into the world of evidence: \texttt{true}
becomes the unit type \texttt{⊤}, and \texttt{false} becomes the empty
type \texttt{⊥}. Operationally, an implicit argument of this type works
as a guard.

\begin{itemize}
\tightlist
\item
  If \texttt{n\ ≤\ m} holds, the type of the implicit value reduces to
  \texttt{⊤}. Agda then happily provides the implicit value.
\item
  Otherwise, the type reduces to \texttt{⊥}, which Agda has no chance of
  providing, so it will throw an error. For instance, if we call
  \texttt{3\ -\ 5} we get \texttt{\_n≤m\_254\ :\ ⊥}.
\end{itemize}

We obtain the witness for \texttt{n\ ≤\ m} using \texttt{toWitness},
which we defined earlier:

\begin{fence}
\begin{code}
_-_ : (m n : ℕ) {n≤m : T ⌊ n ≤? m ⌋} → ℕ
_-_ m n {n≤m} = minus m n (toWitness n≤m)
\end{code}
\end{fence}

We can safely use \texttt{\_-\_} as long as we statically know the two
numbers:

\begin{fence}
\begin{code}
_ : 5 - 3 ≡ 2
_ = refl
\end{code}
\end{fence}

It turns out that this idiom is very common. The standard library
defines a synonym for \texttt{T\ ⌊\ ?\ ⌋} called \texttt{True}:

\begin{fence}
\begin{code}
True : ∀ {Q} → Dec Q → Set
True Q = T ⌊ Q ⌋
\end{code}
\end{fence}

\hypertarget{exercise-false}{%
\subsubsection{\texorpdfstring{Exercise
\texttt{False}}{Exercise False}}\label{exercise-false}}

Give analogues of \texttt{True}, \texttt{toWitness}, and
\texttt{fromWitness} which work with \emph{negated} properties. Call
these \texttt{False}, \texttt{toWitnessFalse}, and
\texttt{fromWitnessFalse}.

\hypertarget{standard-library}{%
\section{Standard Library}\label{standard-library}}

\begin{fence}
\begin{code}
import Data.Bool.Base using (Bool; true; false; T; _∧_; _∨_; not)
import Data.Nat using (_≤?_)
import Relation.Nullary using (Dec; yes; no)
import Relation.Nullary.Decidable using (⌊_⌋; True; toWitness; fromWitness)
import Relation.Nullary.Negation using (¬?)
import Relation.Nullary.Product using (_×-dec_)
import Relation.Nullary.Sum using (_⊎-dec_)
import Relation.Binary using (Decidable)
\end{code}
\end{fence}

\hypertarget{unicode}{%
\section{Unicode}\label{unicode}}

\begin{myDisplay}
∧  U+2227  LOGICAL AND (\and, \wedge)
∨  U+2228  LOGICAL OR (\or, \vee)
⊃  U+2283  SUPERSET OF (\sup)
ᵇ  U+1D47  MODIFIER LETTER SMALL B  (\^b)
⌊  U+230A  LEFT FLOOR (\clL)
⌋  U+230B  RIGHT FLOOR (\clR)
\end{myDisplay}

